plugins {
    id "org.springframework.boot" version "2.7.5"
    id "io.spring.dependency-management" version "1.1.0"
    id "java"
}

group = "ddd-by-example"
version = "1.0.0"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" }
}

dependencies {

    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.kafka:spring-kafka"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "io.micrometer:micrometer-core:1.10.0"
    implementation "io.micrometer:micrometer-registry-prometheus"
    implementation "org.hibernate:hibernate-validator:8.0.0.Final"
    implementation "com.vladmihalcea:hibernate-types-52:2.20.0"
    implementation "com.zaxxer:HikariCP:5.0.1"
    runtimeOnly 'org.postgresql:postgresql:42.5.1'
    annotationProcessor "org.projectlombok:lombok:1.18.24"

    //Tests
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.testcontainers:testcontainers:1.17.4"
    testImplementation "org.testcontainers:kafka:1.17.4"
    testImplementation "org.testcontainers:postgresql:1.17.4"
    testImplementation "org.awaitility:awaitility:4.2.0"
    testImplementation "au.com.dius.pact.consumer:junit5:4.3.16"
    testImplementation "au.com.dius.pact.provider:junit5spring:4.3.16"
    testImplementation "org.skyscreamer:jsonassert:1.5.1"
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation "com.tngtech.archunit:archunit-junit5:1.0.1"
}

configurations {
    compileOnly { extendsFrom annotationProcessor }
    testCompileOnly { extendsFrom annotationProcessor }
    testAnnotationProcessor { extendsFrom annotationProcessor }
}

test {
    useJUnitPlatform()
    jvmArgs("--enable-preview")
}

static defaultSetup(CompileOptions options) {
    options.incremental = true
    options.compilerArgs << "--enable-preview"
    options.compilerArgs << "-parameters"
    options.compilerArgs << "-Xlint:unchecked"
}

compileJava { defaultSetup(options) }
compileTestJava { defaultSetup(options) }
